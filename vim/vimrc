" Plugin manager {{{
set nocompatible
" It is required to turn filetype off while Vundle is running
" due to runtime issues. It is fine to turn it back on after though.
filetype off
set rtp +=~/.vim/bundle/Vundle.vim
call vundle#begin()
" Let Vundle manage Vundle, required
Plugin 'VundleVim/Vundle.vim'
Plugin 'ctrlpvim/ctrlp.vim'
" javascript and jsx syntax highlights
Plugin 'pangloss/vim-javascript'
Plugin 'mxw/vim-jsx'
" go
Plugin 'fatih/vim-go'

call vundle#end()
" It's now fine to turn back on filetype

" }}}
" FileType plugin {{{
filetype plugin on
" Handles opening of vimrc and ftplugin config file (if it exists)
function! OpenConfig()
    " Get the vimfolder
    " Contains the full path
    let plugin_folder = fnamemodify(expand("$MYVIMRC"), ":p:h") . "/ftplugin/"
    let file_name = l:plugin_folder . &filetype . ".vim"
    " usual behavior : open vimrc
    let execute_string =  "split $MYVIMRC"
    if filereadable(l:file_name)
        " if has plugin file, also open it
        let execute_string = l:execute_string . " | vsplit " . l:file_name
    endif
    execute l:execute_string
endfunction
" }}}
" Colors {{{
" maybe remove?
if (&term == "pcterm" || &term == "win32")
  set term=xterm t_Co=256 
  let &t_AB="\e[48;5;%dm"
  let &t_AF="\e[38;5;%dm"
  set termencoding=utf8
  set nocompatible
  inoremap <Char-0x07F> <BS>
  nnoremap <Char-0x07F> <BS>
endif
colorscheme dracula
syntax enable " enable syntax processing
" create custom highlighting groups
augroup coloring
" for some reason ColorScheme does not trigger when you first open vim
" but will trigger on source vimrc
" (and this erases custom highlights due to color scheme)
" hence the double event
    autocmd!
    autocmd ColorScheme,VimEnter * hi User1 ctermbg=212 ctermfg=234
    autocmd ColorScheme,VimEnter * hi User2 ctermbg=141 ctermfg=234
    autocmd ColorScheme,VimEnter * hi User3 ctermbg=215 ctermfg=234
    autocmd ColorScheme,VimEnter * hi User4 ctermbg=84 ctermfg=234 
augroup END
" }}}
" Spaces & Tabs {{{
set tabstop=2 " number of visual spaces per TAB
set softtabstop=2 " number of spaces in tab when editing
set expandtab " replace tabs with spaces
set autoindent " auto indent
set textwidth=100 " max number of character on a line. NOTE: see `formatoptions`
set shiftwidth=2 " number of spaces for each indent step
set autoindent " enable auto indentation
" }}}
" UI {{{
" show relative numbers for all but the current line
set number " show line numbers
set relativenumber " show relative numbers
" set cursorline " highlight current line
set lazyredraw " avoid redraw in the middle of macros
set showmatch " highligh matching [{()}]
set matchtime=2 " number of ms the matching bracket should be highlighted
" always enable status line
set laststatus=2
" }}}
" Searching {{{
set incsearch " search as characters are entered
set hlsearch " highlight matches
set path+=.,**
set wildmenu
" }}}
" Tags {{{
" directly jump if only one, open list instead
nnoremap <c-]> g<c-]>
" more logical jump back
nnoremap <c-[> <c-t>
" use better completion (replaces base completion module)
inoremap <c-n> <c-x><c-]>
" }}}
" Folding {{{
set foldenable " show all folds
" }}}
" General Mappings {{{
let mapleader = " "
let maplocalleader = "-"
" replace the escape key with something more convenient
inoremap jk <esc>
" delete into black hole
nnoremap <leader>d "_d
vnoremap <leader>d "_d
nnoremap <leader>x "_x
vnoremap <leader>x "_x
nnoremap <leader>dd "_dd
" put word in caps in edit mode
inoremap <leader><c-u> <esc>viwUea
" quick access to vimrc and other config files
nnoremap <leader>ev :call OpenConfig()<cr>
" quick source from vimrc
nnoremap <leader>sv :source $MYVIMRC<cr>
" surrounds in normal mode
nnoremap <leader>" bi"<esc>lea"<esc>
nnoremap <leader>' bi'<esc>lea'<esc>
" surronds in visual mode
" the l is necessary because adding one character messes up the `>
vnoremap <leader>{ <esc>`<i{<esc>`>la}<esc>
vnoremap <leader>( <esc>`<i(<esc>`>la)<esc>
vnoremap <leader>[ <esc>`<i[<esc>`>la]<esc>
vnoremap <leader>' <esc>`<i'<esc>`>la'<esc>
vnoremap <leader>" <esc>`<i"<esc>`>la"<esc>
" move to end of line
nnoremap L $
vnoremap L $
" move to start of line
nnoremap H 0
vnoremap H 0
" move around splits
nnoremap <leader>h <c-w>h
nnoremap <leader>k <c-w>k
nnoremap <leader>j <c-w>j
nnoremap <leader>l <c-w>l
" duplicate line
nnoremap <leader>dl Vyp
" use \v (very magic) mode on searches
nnoremap / /\v
nnoremap ? ?\v
" clear all search AND match pattern
nnoremap <leader>cs :match none<cr>:nohlsearch<cr>
" open previous buffer in vsplit
nnoremap <leader>vsP :execute "rightbelow vsplit" bufname("#")<cr>
" open previous buffer in current window
nnoremap <leader>P :execute "edit " . bufname("#")<cr> " go to previous and next quickfix entry
" navigate in the quickfix list
nnoremap <leader>n :cnext<cr>
nnoremap <leader>p :cprevious<cr>
" }}}
" Movement Mappings {{{
" In Next (
onoremap in( :<c-u>normal! f(vi(<cr>
" In Last (
onoremap il( :<c-u>normal! F)vi(<cr>
" In Next {
onoremap in{ :<c-u>normal! f{vi{<cr>
" In Last {
onoremap il{ :<c-u>normal! f}vi{<cr>
" In Next [
onoremap in[ :<c-u>normal! f[vi[<cr>
" In Last [
onoremap il[ :<c-u>normal! f]vi[<cr>
" In Next "
onoremap in" :<c-u>normal! f"vi"<cr>
" In Last "
onoremap il" :<c-u>normal! F"vi"<cr>
" In Next '
onoremap in' :<c-u>normal! f'vi'<cr>
" In Last '
onoremap il' :<c-u>normal! F'vi'<cr>

" function name
onoremap f :<c-u>normal! 0f(hviw<cr>
" variable name (assumes one space before the '=')
onoremap v :<c-u>normal! 0f=hhviw<cr>
" }}}
" General Abbreviations {{{
" }}}
" Status Line {{{
set statusline=%1*\ " Space and start color
set statusline+=%m  " Modified flag
set statusline+=\ %* " Space and end color 
set statusline+=%2*\ " Space and start color 
set statusline+=*%f " Path to file 
set statusline+=%= " align right
set statusline+=\ %* " space and end color
set statusline+=%3*\ " space and start color 
set statusline+=Line:\ " description 
set statusline+=%-12(%l/%L%) " current line/total lines
set statusline+=\ %* " Space and end color
set statusline+=%4*\ " Space and start color 
set statusline+=%P " Percent of file 
set statusline+=\ %* " Space and end color

" }}}
" Binary Files {{{
augroup Binary
    autocmd!
	autocmd BufReadPost *.bin, *.nes set ft=xxd
augroup END
" }}}
" Plugin config {{{
" ignore maven build files in ctrlp finder
let g:ctrlp_custom_ignore='\v[\/](target)' 
" }}}
"
" TEST SECTION {{{
function! UpdateTags()
  let f = expand("%:p")
  let cmd = 'ctags -a ' . f
  let resp = system(cmd)
  echom resp
endfunction

function! ResetTags()
  let cmd = 'ctags -R .'
  let resp = system(cmd)
endfunction
" }}}
